apply plugin: 'groovy'
apply plugin: 'eclipse'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.5"
        classpath "org.ajoberstar:gradle-git:${grgitVersion}"
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:${nexusStagingVersion}'
    }
}
apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.ajoberstar.grgit"
apply plugin: 'io.codearte.nexus-staging'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.ajoberstar:gradle-git:${grgitVersion}'
    compile 'org.jboss.apiviz:apiviz:1.3.2.GA'
    compile 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:${nexusStagingVersion}'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext {
    def vfile = new File("${projectDir}/version.info")
    try {
        def currentSha = grgit.head().id
        def branchMap = [:]
        /*
         * Check if it is a tagged version
         */
        def tags = grgit.tag.list()
        def tag = tags.find() { it.commit.id == currentSha }
        if (tag == null) {
            project.version = "${project.version}-${grgit.head().abbreviatedId}".take(20)
        } else if (tag.name == project.version){
            println "This is tagged as the official version ${version}"
        } else {
            project.version = "${project.version}-${tag.name}-${grgit.head().abbreviatedId}".take(20)
        }
        println "Due to your git repo status, the project version is detected as ${project.version}"
        vfile.text = project.version
    } catch (Exception ex) {
        ex.printStackTrace()
        println("No Git repository info available, falling back to file")
        if (vfile.exists()) {
            println("No version file, using project version variable as-is")
            version = vfile.text
        }
    }
}


jar {
    baseName "$artifactId"
    manifest {
        attributes 'Implementation-Title': artifactId, 'Implementation-Version': version
    }
}

task wrapper(type: Wrapper) { gradleVersion = gradleVersionToUse }

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName "$artifactId"
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName "$artifactId"
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

pluginBundle {
    website = 'https://github.com/DanySK/build-commons'
    vcsUrl = 'https://github.com/DanySK/build-commons'
    description = 'A rich build configuration, shareable across projects'
    tags = ['pre-configuration', 'configuration', 'javadoc', 'jar', 'pmd', 'findbugs', 'checkstyle']

    plugins {
        buildcommonsPlugin {
            id = 'org.danilopianini.build-commons'
            displayName = 'build-commons'
        }
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

signing {
    sign configurations.archives
}
signArchives.onlyIf { Boolean.parseBoolean(signArchivesIsEnabled) }

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name artifactId
                description projectDescription
                packaging 'jar'
                url "$scmRootUrl/$artifactId"
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                            name 'Danilo Pianini'
                            email 'danilo.pianini@gmail.com'
                            url 'http://www.danilopianini.org/'
                    }
                }
                scm {
                    url "$scmRootUrl/$artifactId"
                    connection "$scmType:$scmLogin/$scmRepoName"
                    developerConnection "$scmType:$scmLogin/$scmRepoName"
                }
            }
        }
    }
}



defaultTasks 'clean', 'build', 'check', 'assemble', 'install', 'javadoc'
